module AMM
{
    struct FMA_Location
    {
        long FMAID;
        string description;
    };

    struct UUID
    {
        long long UUID_MSB;
        long long UUID_LSB;
    };

    enum AssessmentValue {
        OMISSION_ERROR
       ,COMMISSION_ERROR
       ,EXECUTION_ERROR
       ,SUCCESS
    };
    struct Assessment
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         */
    {
        UUID id;
        UUID event_id;
        AssessmentValue value;
        string comment;
    };

    enum EventAgentType {
        LEARNER
       ,INSTRUCTOR
       ,SCENARIO
       ,PHYSIOLOGY
    }

    struct EventFragment
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Volatile
         */
    {
        UUID id;
        unsigned long long timestamp;
        FMA_Location location;
        UUID agent_id;
        EventAgentType agent_type;
        string type;
        string data;
    };

    struct EventRecord
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local (In case of module disconnect/reconnect)
         *  Liveliness: Automatic, 1 second lease
         */
    {
        UUID id;
        unsigned long long timestamp;
        FMA_Location location;
        UUID agent_id;
        EventAgentType agent_type;
        string type;
        string data;
    };

    enum FAR_Status {
        REQUESTING
       ,ACCEPTED
       ,REJECTED
    };
    struct FragmentAmendmentRequest
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Volatile
         */
    {
        UUID id;
        UUID fragment_id;
        FAR_Status status;
        // Values that can be amended
        unsigned long long timestamp;
        FMA_Location location;
        UUID agent_id;
        EventAgentType agent_type;
        string type;
        string data;
    };

    enum LogLevel {
        FATAL
       ,ERROR
       ,WARN
       ,INFO
       ,DEBUG
       ,TRACE
    };
    struct Log
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         */
    {
        unsigned long long timestamp;
        @Key UUID module_id;
        LogLevel level;
        string message;
    };

    struct ModuleConfiguration
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         */
    {
        string name;
        @Key UUID module_id;
        UUID educational_encounter;
        unsigned long long timestamp;
        octet ip_address[4];
        string capabilities_configuration;
    };

    struct OmittedEvent
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local (In case of module disconnect/reconnect)
         *  Liveliness: Automatic, 1 second lease
         */
    {
        UUID id;
        unsigned long long timestamp;   // When the omission was detected.
        FMA_Location location;
        UUID agent_id;
        EventAgentType agent_type;
        string type;
        string data;
    };

    struct Semantic_Version {
        unsigned short major;
        unsigned short minor;
        unsigned short patch;
    };
    struct OperationalDescription
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         */
    {
        string name;
        string description;
        string manufacturer;
        string model;
        string serial_number;
        @Key UUID module_id;
        string module_version;
        Semantic_Version configuration_version;
        Semantic_Version AMM_version;
        string capabilities_schema; // Defined by CapabilitiesSchema.xsd
    };

    struct PhysiologyModification
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         */
    {
        UUID id;
        UUID event_id;
        string type;
        string data;
    };

    struct PhysiologyValue
        /** QoS:
         *  Reliability: Best Effort
         *  Durability: Transient Local
         *  Ownership: Exclusive
         *  Ownership Strength: Set via Configuration if non-zero
         *  Presentation: Access Scope: Instance, Coherent Access: True, Order Access: False
         *  Liveliness: Automatic, 1 second lease
         */
    {
        long long simulation_time;
        unsigned long long timestamp;
        @Key string name;   // BioGears node path
        string unit;
        double value;
    };

    struct PhysiologyWaveform // Reliable delivery
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         *  Ownership: Exclusive
         *  Ownership Strength: Set via Configuration if non-zero
         *  Liveliness: Automatic, 1 second lease (Lower if feasible, requires testing)
         */
    {
        long long simulation_time;
        unsigned long long timestamp;
        @Key string name;   // BioGears node path
        string unit;
        double value;
    };

    struct RenderModification
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         */
    {
        UUID id;
        UUID event_id;
        string type;
        string data;
    };

    enum ControlType {
        RUN
       ,HALT
       ,RESET
       ,SAVE
    };
    struct SimulationControl
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         *  Liveliness: Automatic, 1 second lease
         */
    {
        unsigned long long timestamp;
        ControlType type;
        UUID educational_encounter;
    };

    enum StatusValue {
        OPERATIONAL
       ,INOPERATIVE
       ,EXIGENT
    };
    struct Status
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         *  Liveliness: Automatic, 1 second lease
         */
    {
        @Key UUID module_id;
        string module_name;
        UUID educational_encounter;
        @Key string capability;
        unsigned long long timestamp;
        StatusValue value;
        string message;
    };
};
